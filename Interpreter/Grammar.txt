Program => PrgrmStmts

BASICS BUILDING BLOCKS
--------------------------------------------------------------------------------

IntDigit   => [0-9]
IntDigits  => IntDigit IntDigits
           |  empty

HexDigit   => [0-9a-fA-F]
HexDigits  => HexDigit HexDigits
           |  empty

BinDigit   => [0-1]
BinDigits  => BinDigit BinDigits
           |  empty

Ident       => [a-zA-Z_$][a-zA-Z0-9_?!$]*

OptIdent    => Ident | empty

KeyVal      => (Ident | StringLit) ':' Expr

Expr        => HeadExpr TailExpr
HeadExpr    => NewStmt
            |  InstanceVar
            |  ClassVar
            |  YieldExpr
            |  BuiltinLit
            |  SquareBracInd
            |  FuncCall
            |  IncDecStmt
            |  Ident
            |  '(' Expr ')'
TailExpr    => InfixOp Expr
            |  empty

OptExpr     => Expr | empty

Stmt        => VarDecl
            |  ReturnStmt
            |  SpawnStmt
            |  AssignStmt
            |  ControlStructure
            |  Yield Expr
            |  FuncCall
            |  NewStmt
            |  IncDecStmt
            |  Comment

PrgrmStmt   => VarDecl
            |  SpawnStmt
            |  ControlStructure
            |  LanguageConstruct
            |  NewStmt
            |  Comment
            |  AssignStmt
            |  FuncCall
            |  IncDecStmt


ChanSendRcv => ChanSend | ChanRcv
ChanSend    => Expr '->' Expr
ChanRcv     => OptIdent '<-' Expr

Constructor => Ident '(' OptIdentList ')' '{' Stmts '}'

ClassVarDecl => ClassVar '=' Expr

Comment         => SingleLnComment | MultiLnComment
SingleLnComment => '//' [^\n]*
MultiLnComment  => '/*' [^(*/)]* '*/'

Params      => '(' OptExprs ')'
OptParams   => Params | empty

FuncCall    => DottedExprs Params

SquareBracInd => Ident '[' Expr ']'

VarDecl     => 'var' Ident optRSAssign

RSAssign    => '=' Exprs

OptRSAssign => RSAssign | empty

AssignStmt  => IdentList RSAssign

ReturnStmt  => 'return' OptExprs

InstanceVar => ('@_' | '@') Ident

ClassVar    => ('#_' | '#') Ident

YieldExpr   => 'yield' OptExprs

LISTS
--------------------------------------------------------------------------------

PrgrmStmts  => PrgrmStmt PrgrmStmts!
PrgrmStmts! => PrgrmStmt
            |  empty

IdentList   => Ident IdentList!
IdentList!  => ',' IdentList
            |  empty

OptIdentList => IdentList | empty

Stmts       => Stmt Stmts!
Stmts!      => ';' Stmts
            |  empty

Exprs       => Expr Exprs!
Exprs!      => ',' Exprs
            |  empty

OptExprs    => Exprs | empty

DottedExprs  => Expr DottedExprs!
DottedExprs! => '.' DottedExprs
             |  empty

KeyValList  => KeyVal KeyValList!
KeyValList! => ',' KeyValList
            |  empty

CONTROL STRUCTURES
--------------------------------------------------------------------------------

ControlStructure => ForInLoop | WhileLoop | IfElse | GivenIs | WaitFor

ForInLoop   => 'for' IdentList 'in' Expr 'do' Stmts 'end'

WhileLoop   => 'while' Expr 'do' Stmts 'end'

IfElse       => IfBranch ElseIfBranch
IfBranch     => 'if' Stmts 'do' Stmts
ElseIfBranch => 'else if' Stmts ElseIfBranch
             |  ElseBranch
             |  'end'
ElseBranch   => 'else' Stmts 'end'

GivenIs     => 'given' Expr IsBranch
IsBranch    => 'is' Expr 'do' Stmts IsBranch
            |  ElseBranch
ElseBranch  => 'else do' Stmts 'end'
            |  'end'

WaitFor     => 'wait_for' 'either' ChanSendRcv 'then' Stmts OrBranch 'end'
OrBranch    => 'or' ChanSendRcv 'then' Stmts OrBranch
            |  OrDefault
OrDefault   => 'or do' Stmts 'end'
            |  empty

BUILT IN LITERALS
--------------------------------------------------------------------------------

BuiltinLit => IntLit
           |  FloatLit
           |  StringLit
           |  RegexLit
           |  ArrayLit
           |  HashLit
           |  ChanLit
           |  ClosureLit

IntLit     => IntDigits | HexLit | BinLit
HexLit     => '0' ('x' | 'X') HexDigits
BinLit     => '0' ('b' | 'B') BinDigits

FloatLit   => IntDigits '.' IntDigits

StringLit  => SingleStr | DoubleStr
SingleStr  => "'" [^']* "'"
DoubleStr  => '"' [^"]* '"'

RegexLit   => '/' ([^/]|\.)+ '/'

ArrayLit   => '[' Exprs ']'

HashLit    => '{' KeyValList '}'

ChanLit    => '<-' OptExpr '->'

ClosureLit  => OptAsterick ClosureDef
OptAsterick => '*'
            |  empty
ClosureDef  => '(' OptIdentList ')' '{' Stmts '}' OptParams

LANGUAGE CONSTRUCTS
--------------------------------------------------------------------------------

LanguageConstruct => FuncDef | ClassDef | ImportStmt | ExportStmt

ClassDef       => 'class' Ident ExtendInfo ClassInternals 'end'
ExtendInfo     => 'extends' IdentList
ClassInternals => (Constructor | ClassVarDecl | FuncDef | Comment) ClassInternals
               |  empty

FuncDef        => 'gen' 'function' Ident '(' OptIdentList ')' '{' Stmts '}'
               |  'function' Ident '(' OptIdentList ')' '{' Stmts '}'

SpawnStmt      => 'spawn' (ClosureDef | FuncCall)

NewStmt        => 'new' FuncCall

ImportStmt     => 'import' (FullImport | PartialImport | MultiImport)
FullImport     => PkgLocation AsRename
PartialImport  => Ident 'from' PkgLocation AsRename
MultiImport    => IdentList 'from' PkgLocation
PkgLocation    => ('std' | 'pkg') '::' Ident
               |  ImportPath
ImportPath     => ('~' | '.') '/' Path
Path           => Ident '/' Path
               |  Ident '.hy'
AsRename       => 'as' Ident
               |  empty

ExportStmt     => 'export' (SingleExport | MultiExport)
MultiExport    => IdentList
SingleExport   => Ident AsRename


OPPERATOR EXPRESSIONS
--------------------------------------------------------------------------------

IncDecStmt     => Ident ( '++' | '--' )

TailAriExpr    => ArithOp Expr
               |  empty

StringExpr     => StringLit StringTail
StringTail     => '+' StringLit
               | empty

InfixOp        => InfixEq
               |  ArithOp
               |  '==' | '!=' | '<' | '<=' | '>' | '>='
               |  '&&'
               |  OrOp
               |  RangeOps

InfixEq        => InfixEq! '='
InfixEq!       => ArithOp
               |  OrOp

ArithOp        => MulOp | AddOp

MulOp          => '*' | '/' | '^' | '%' | '&' | '|'
               |  ShiftOp
ShiftOp        => '<<' | '>>'
AddOp          => '+' | '-'
OrOp           => '||'

RangeOps       => '...' | '..'